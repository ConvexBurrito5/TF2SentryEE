## What is the nRF24L01+

For this project I have chosen to use the nRF24L01+ radio chip. 

![CHIP](https://europe1.discourse-cdn.com/arduino/original/4X/b/4/2/b421482fb2f9c697f6eecdac9b9c871046985499.png)

It is a 2.4GHz radio chip that can be configured to communicate on different channels.

As an amateur I have fallen on the sword enough to have a pretty good idea of how to debug
*most* setups of this chip. This is a documentation of my pitfalls and how I have been able to debug them.

### Where do I buy the chip?
Amazon, There are multiple versions out there. I have found for my purposes that 
the simple [NRF24L01+ Wireless Transceiver Module2.4G](https://www.amazon.com/HiLetgo-NRF24L01-Wireless-Transceiver-Module/dp/B00LX47OCY/ref=sr_1_4_pp?crid=2KWMZMRZ0UILO&dib=eyJ2IjoiMSJ9.INY1ZJf_cbNGizzyPvyfDS0CbZODjG_N0mS7xCN63KNWL4OH8n7-hqUnpavVAg43KOz49tCFTViAHrluT9jkeXvjJIyY3Q7CrtS0wHB40oUkOpWzFD75RPOFgLzrV8B0MKg82vtUBQIg73r8a_yQgPJEH3L7_FdfylGQ2omRGDx4yG7ffC4-dElc8kUZDYgvaaYUFaheKtv7lZELJZf_waOo2-QU2kXIVS6YqDIrXJU.ajWF2BDEuevMZLjIB7-jcC_7pyVp5YgVnWn5GX9lVBA&dib_tag=se&keywords=nRF24L01%2B&qid=1724766992&sprefix=nrf24l01%2B%2Caps%2C90&sr=8-4) WITHOUT the SMA
adapter and antenna is perfectly ok since I am keeping it inside and within close line of sight.

### How do I configure the chip?
Before hooking up the full turret pinout it's advisable to run the following two tests.

### Testing communication between the nRF24L01+ and the Aurduino


The pinout for an AURDUINO UNO:

![DEMOPINOUT](https://europe1.discourse-cdn.com/arduino/original/4X/4/8/f/48ff0dc9c5cba648cf0bd7879e8bad7be9bfbe36.png)

The following Aurduino script tests to verify that the Aurduino is communicating with the
nRF24L01+ chip. [Code from FORUM POST](https://forum.arduino.cc/t/simple-nrf24l01-2-4ghz-transceiver-demo/405123).

    // 18 Mar 2018 - simple program to verify connection between Arduino
    //      and nRF24L01+
    //  This program does NOT attempt any communication with another nRF24
    
    #include <SPI.h>
    #include <nRF24L01.h>
    #include <RF24.h>
    
    #include <printf.h>
    
    #define CE_PIN   9
    #define CSN_PIN 10
    
    const byte thisSlaveAddress[5] = {'R','x','A','A','A'};
    
    RF24 radio(CE_PIN, CSN_PIN);
    //Possible fix if you have a NRF24L01 Module that is incompatable with Default
    //10MHz SPI clock speed because of 1K 'protection' resistors. - "6v6gt"
    //Swap line 20 for line 16
    //RF24 radio(CE_PIN, CSN_PIN, 4000000);
    
    char dataReceived[10]; // this must match dataToSend in the TX
    bool newData = false;
    
    
    void setup() {
        Serial.begin(9600);
        printf_begin();
    
        Serial.println("CheckConnection Starting");
        Serial.println();
        Serial.println("FIRST WITH THE DEFAULT ADDRESSES after power on");
        Serial.println("  Note that RF24 does NOT reset when Arduino resets - only when power is removed");
        Serial.println("  If the numbers are mostly 0x00 or 0xff it means that the Arduino is not");
        Serial.println("     communicating with the nRF24");
        Serial.println();
        radio.begin();
        radio.printDetails();
        Serial.println();
        Serial.println();
        Serial.println("AND NOW WITH ADDRESS AAAxR  0x41 41 41 78 52   ON P1");
        Serial.println(" and 250KBPS data rate");
        Serial.println();
        radio.openReadingPipe(1, thisSlaveAddress);
        radio.setDataRate( RF24_250KBPS );
        radio.printDetails();
        Serial.println();
        Serial.println();
    }
    
    
    void loop() {
    
    }

The output of this script should look something like this

    FIRST WITH THE DEFAULT ADDRESSES after power on
      Note that RF24 does NOT reset when Arduino resets - only when power is removed
      If the numbers are mostly 0x00 or 0xff it means that the Arduino is not
         communicating with the nRF24
    
    SPI Speedz	= 10 Mhz
    STATUS		= 0x0e RX_DR=0 TX_DS=0 MAX_RT=0 RX_P_NO=7 TX_FULL=0
    RX_ADDR_P0-1	= 0xe7e7e7e7e7 0x4141417852
    RX_ADDR_P2-5	= 0xc3 0xc4 0xc5 0xc6
    TX_ADDR		= 0xe7e7e7e7e7
    RX_PW_P0-6	= 0x20 0x20 0x20 0x20 0x20 0x20
    EN_AA		= 0x3f
    EN_RXADDR	= 0x03
    RF_CH		= 0x4c
    RF_SETUP	= 0x07
    CONFIG		= 0x0e
    DYNPD/FEATURE	= 0x00 0x00
    Data Rate	= 1 MBPS
    Model		= nRF24L01+
    CRC Length	= 16 bits
    PA Power	= PA_MAX
    ARC		= 0
    
    
    AND NOW WITH ADDRESS AAAxR  0x41 41 41 78 52   ON P1
     and 250KBPS data rate
    
    SPI Speedz	= 10 Mhz
    STATUS		= 0x0e RX_DR=0 TX_DS=0 MAX_RT=0 RX_P_NO=7 TX_FULL=0
    RX_ADDR_P0-1	= 0xe7e7e7e7e7 0x4141417852
    RX_ADDR_P2-5	= 0xc3 0xc4 0xc5 0xc6
    TX_ADDR		= 0xe7e7e7e7e7
    RX_PW_P0-6	= 0x20 0x20 0x20 0x20 0x20 0x20
    EN_AA		= 0x3f
    EN_RXADDR	= 0x03
    RF_CH		= 0x4c
    RF_SETUP	= 0x27
    CONFIG		= 0x0e
    DYNPD/FEATURE	= 0x00 0x00
    Data Rate	= 250 KBPS
    Model		= nRF24L01+
    CRC Length	= 16 bits
    PA Power	= PA_MAX
    ARC		= 0

The two signs you are looking for to ensure that your hardware is setup correctly
is that most of your register values do not = 0x00 and the Data Rate updates 
from '1 MBPS' to '250 KBPS'.

### What can I do to troubleshoot?
There are multiple things you can do to troubleshoot your outputs.

1) Verify your pinout. Humor me and check again.
2) Use an external power supply, I use [3.3V 5V Power Supply Module for MB102 102 Prototype Breadboard DC 6.5-12V](https://www.amazon.com/dp/B00HJ6AE72?ref=ppx_yo2ov_dt_b_fed_asin_title).
   3) IF YOU USE AN EXTERNAL POWER SUPPLY. Place a common ground wire between the ground of your Aurduino and the ground of the power supply. I was an amateur and did not realize this. [FORUM POST](https://forum.arduino.cc/t/issues-getting-nrf24l01-to-communicate/1293212/8).
3) Soldering a 10uF Capacitor across the power and ground pins of the nRF24L01+ could help you at this step.
4) If all else fails. Like it did for me, I de-soldered the dupont connectors from the nRF24L01+ chip and instead of using [rainbow cables](https://www.amazon.com/EDGELEC-Breadboard-Optional-Assorted-Multicolored/dp/B07GD1XFWV/ref=sr_1_3?crid=5Q8XBMHWTG9O&dib=eyJ2IjoiMSJ9.CHoqNulWyhwFABWE62p3Eeclu_gnmNSY34iUgsVSnoXCW7neNStUU9-pRbavady3n02GZL2EqFWVYskcfod0MevfH0spzhEXQyxPkmSqZuXr7t8TAe9aK07xoXNq-vq-Ixv2QjPgCLgpfZVFPmlwCr2AgfnriZGEJU8cakekAdXQMwr921H8GlSnLD7sOpcBHXUWf7NFye2lJy2p-Ky1WnEO2f8wFirsoPE76FkhM0Y.KeFG_PwtQFbAlmcvzKQ72I7Nz84EyV2nIq3DthXY5yI&dib_tag=se&keywords=breadboard+rainbow+cable&qid=1724768951&sprefix=breadboard+rainbow+cabl%2Caps%2C98&sr=8-3). I soldered to the chip a length of [22 AWG solid core hookup wire](https://www.amazon.com/gp/product/B07TX6BX47/ref=ppx_yo_dt_b_search_asin_title?ie=UTF8&psc=1). This was my final solution to bookend all of my problems with this chip. [FORUM POST](https://forum.arduino.cc/t/further-nrf24l01-problems/1295216).
4) Its worth mentioning that if all of the above fails you could have bought a nRF24L01+ chip with protection resistors in series. To debug this you have to lower the default SPI Speed and modify the above demo script (See line 16-20 in the script). [FORUM RESPONSE](https://forum.arduino.cc/t/issues-getting-nrf24l01-to-communicate/1293212/8?u=convexburrito5) 

### At this point you should be passing the above test script on both your 'turret (receiver)' and 'wrangler (transmitter)' Aurduinos.
The next step is to flash the following scripts onto each Aurduino in order to verify that they can communicate with each other. [Code from FORUM POST](https://forum.arduino.cc/t/simple-nrf24l01-2-4ghz-transceiver-demo/405123)

Wrangler (Transmitter)

    // SimpleTx - the master or the transmitter
    
    #include <SPI.h>
    #include <nRF24L01.h>
    #include <RF24.h>
    
    
    #define CE_PIN   9
    #define CSN_PIN 10
    
    const byte slaveAddress[5] = {'R','x','A','A','A'};
    
    
    RF24 radio(CE_PIN, CSN_PIN); // Create a Radio
    
    char dataToSend[10] = "Message 0";
    char txNum = '0';
    
    
    unsigned long currentMillis;
    unsigned long prevMillis;
    unsigned long txIntervalMillis = 1000; // send once per second
    
    
    void setup() {
    
        Serial.begin(9600);
    
        Serial.println("SimpleTx Starting");
    
        radio.begin();
        radio.setDataRate( RF24_250KBPS );
        radio.setRetries(3,5); // delay, count
        radio.openWritingPipe(slaveAddress);
    }
    
    //====================
    
    void loop() {
        currentMillis = millis();
        if (currentMillis - prevMillis >= txIntervalMillis) {
            send();
            prevMillis = millis();
        }
    }
    
    //====================
    
    void send() {
    
        bool rslt;
        rslt = radio.write( &dataToSend, sizeof(dataToSend) );
            // Always use sizeof() as it gives the size as the number of bytes.
            // For example if dataToSend was an int sizeof() would correctly return 2
    
        Serial.print("Data Sent ");
        Serial.print(dataToSend);
        if (rslt) {
            Serial.println("  Acknowledge received");
            updateMessage();
        }
        else {
            Serial.println("  Tx failed");
        }
    }
    
    //================
    
    void updateMessage() {
            // so you can see that new data is being sent
        txNum += 1;
        if (txNum > '9') {
            txNum = '0';
        }
        dataToSend[8] = txNum;
    }

Receiver (Turret)

    // SimpleRx - the slave or the receiver
    
    #include <SPI.h>
    #include <nRF24L01.h>
    #include <RF24.h>
    
    #define CE_PIN   9
    #define CSN_PIN 10
    
    const byte thisSlaveAddress[5] = {'R','x','A','A','A'};
    
    RF24 radio(CE_PIN, CSN_PIN);
    
    char dataReceived[10]; // this must match dataToSend in the TX
    bool newData = false;
    
    //===========
    
    void setup() {
    
        Serial.begin(9600);
    
        Serial.println("SimpleRx Starting");
        radio.begin();
        radio.setDataRate( RF24_250KBPS );
        radio.openReadingPipe(1, thisSlaveAddress);
        radio.startListening();
    }
    
    //=============
    
    void loop() {
        getData();
        showData();
    }
    
    //==============
    
    void getData() {
        if ( radio.available() ) {
            radio.read( &dataReceived, sizeof(dataReceived) );
            newData = true;
        }
    }
    
    void showData() {
        if (newData == true) {
            Serial.print("Data received ");
            Serial.println(dataReceived);
            newData = false;
        }
    }

If you monitor the Transmitter code it should ping "Data sent # Acknowledge received".

If this is not happening, go back up and verify that the first demo script is giving a correct output. If the above script is 100% working
 you could be running into a problem of a noisy airspace. Is your microwave running right next to this? Is your router turned on and right next to this? They could be using the same band. Look into A) Moving the project or B) Changing the channel that the chip is running on.

### How do I further refine my data?
If you have a lot of noise in your outputs you should strongly consider soldering a 10uF capacitor across the power and ground pins of your chip.

Upon doing this I virtually eliminated the noise in my setup.

## What now?
If you have a green light on both tests, go back to the Readme.md file and continue on the journey of building the turret.

If you are implementing this chip for a different project, I strongly suggest further reading on the limits of this chip.
1) [A brief overview](https://forum.arduino.cc/t/simple-nrf24l01-2-4ghz-transceiver-demo/405123)
2) [Online DOCS](https://nrf24.github.io/RF24/)
